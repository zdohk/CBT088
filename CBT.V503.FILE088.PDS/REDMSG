SYP1ZRC  CSECT                                                          SPO00007
**********************************************************************
**                                                                  **
** "SYP1ZRC" - PROGRAM TO DISPLAY A MESSAGE OF THE USER'S CHOICE    **
**             ON THE MVS CONSOLE.  ON A COLOR CONSOLE THE MESSAGE  **
**             APPEARS IN RED.                                      **
**                                                                  **
** THIS PROGRAM IS INTENDED TO RUN IN A JOBSTREAM ONLY IF A         **
** PREVIOUS STEP(S) PASSED A NON-ZERO RETURN CODE.  IT WILL         **
** DISPLAY A MESSAGE FROM THE PARM FIELD ON THE OPERATOR'S          **
** CONSOLE IN RED!                                                  **
**                                                                  **
** **************************************************************** **
**                                                                  **
** TYPICAL USAGE WITHIN A CATALOGED PROCEDURE:                      **
**                                                                  **
**//STEP3   EXEC PGM=ADRDSSU                                        **
**  --- MORE JCL ----                                               **
**//STEP4   EXEC PGM=SYP1ZRC,REGION=4K,COND=(0,EQ,STEP3),           **
**// PARM=' WARNING --- " &VOL " BACKUP HAD A NON-ZERO RETURN CODE.'**
**                                                                  **
**  NOTE THE USAGE OF A SYMBOLIC WITHIN THE PARM (MESSAGE), WHICH   **
**  ALLOWS MORE FLEXIBILITY WITHIN THE MESSAGE TEXT.                **
**                                                                  **
**  THIS PARTICULAR EXAMPLE WILL DISPLAY A RED MESSAGE IF DF/DSS    **
**  COMPLETES WITH A NON-ZERO RETURN CODE.                          **
**                                                                  **
**********************************************************************
**                                                                  **
**  AUTHOR:             BRUCE BUTLER                                **
**  COMPANY:            ISC SYSTEMS CORP.                           **
**  ADDRESS:            P.O. BOX TAF C-8                            **
**  LOCATION:           SPOKANE, WASHINGTON 99220                   **
**  PHONE:              (509) 922-6275                              **
**                                                                  **
**********************************************************************
         STM   R14,R12,12(R13) SAVE REGISTERS                           SPO00008
         LR    R12,R15        USE R12 FOR BASE, NOT R15                 SPO00009
         USING SYP1ZRC,R12                                              SPO00010
         LR    R2,R13         SAVE CALLERS R13, SET UP A SAVEAREA       SPO00011
         LA    R13,SAVEAREA   ADDRESS OF OUR SAVEAREA                   SPO00012
         ST    R13,8(,R2)     TELL CALLER WHERE OUR SAVEAREA IS         SPO00013
         ST    R2,SAVEAREA+4  TELL US WHERE CALLERS SAVEAREA IS         SPO00014
         L     R2,0(R1)           LOAD ADDRESS OF DATA                  PAR00030
         LH    R3,0(R2)           LOAD LENGTH OF DATA                   PAR00040
         LTR   R3,R3              CHECK FOR PRESENCE OF PARM FIELD      PAR00010
         BNZ   CKLEN              ..OK                                  PAR00020
         MVC   PARM,DEFAULT       MOVE IN DEFAULT MESSAGE
         B     DOIT
CKLEN    CH    R3,=H'70'          Q. OVER MAX LENGTH
         BNH   MOVEIT             ..NO
         LH    R3,=H'70'          ..YES, TRUNCATE AT 70
MOVEIT   BCTR  R3,0               SUBTRACT 1, FOR EXECUTE
         LA    R2,2(,R2)          POINT DATA ADDRESS AROUND LENGTH      PAR00050
         EX    R3,EXMOVE          MOVE DATA
DOIT     WTO   MF=(E,WTOLIST)     DO THE WTO
EOJ      LA    R15,0              PICKUP R/C                            SPO00074
         L     R13,SAVEAREA+4 RESTORE CALLERS R13                       SPO00076
         LM    R0,R12,20(R13) RESTORE CALLERS REGISTERS                 SPO00077
         L     R14,12(R13)    RESTORE CALLERS R14                       SPO00078
         BR    R14            ISSUE RETURN                              SPO00079
         SPACE 3                                                        SPO00085
EXMOVE   MVC   PARM(0),0(R2)
DEFAULT DC CL70'A NON-ZERO RETURN CODE HAS BEEN DETECTED IN THIS JOB STX
               REAM'
         SPACE 2
*        1         2         3         4         5         6         7
*2345678901234567890123456789012345678901234567890123456789012345678901
         SPACE 2
WTOLIST  WTO   '         1         2         3         4         5     X
                   6         7',                                       X
               DESC=(11),ROUTCDE=(1,2,3,4,10),MF=L
         SPACE 1
WTOLISTE DS    0D
         ORG   WTOLIST+4
PARM     DC    CL70' '           PARM FIELD
         ORG   WTOLISTE
SAVEAREA DS    18F                                                      SPO00118
         LTORG                                                          SPO00119
         SPACE 1                                                        SPO00121
R0   EQU 0                                                              SPO00123
R1   EQU 1                                                              SPO00124
R2   EQU 2                                                              SPO00125
R3   EQU 3                                                              SPO00126
R4   EQU 4                                                              SPO00127
R5   EQU 5                                                              SPO00128
R6   EQU 6                                                              SPO00129
R7   EQU 7                                                              SPO00130
R8   EQU 8                                                              SPO00131
R9   EQU 9                                                              SPO00132
R10  EQU 10                                                             SPO00133
R11  EQU 11                                                             SPO00134
R12  EQU 12                                                             SPO00135
R13  EQU 13                                                             SPO00136
R14  EQU 14                                                             SPO00137
R15  EQU 15                                                             SPO00138
         SPACE 1                                                        SPO00140
         END                                                            SPO00141
